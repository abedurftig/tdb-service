buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.hidetake:gradle-swagger-generator-plugin:2.9.0"
    }
}

//apply plugin: "org.hidetake.swagger.generator"
apply plugin: org.hidetake.gradle.swagger.generator.SwaggerGeneratorPlugin

repositories {
    jcenter()
}

dependencies {

    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.3'
    swaggerUI 'org.webjars:swagger-ui:2.2.6'

    compile('org.webjars:jquery:3.2.1')
    compile('io.swagger:swagger-annotations:1.5.16')
    compile('io.springfox:springfox-swagger2:2.7.0')
    compile('io.springfox:springfox-swagger-ui:2.7.0')

}

swaggerSources {
    applicationApi {
        inputFile = file('swagger/api-spec.yml')
        code {
            language = 'spring'
            configFile = file('swagger/swagger-conf.json')
            // Validate YAML before code generation
            dependsOn validation

        }
        ui {
            options.docExpansion = 'full'
        }
    }
}

// Configure compile task dependency and source
compileJava.dependsOn swaggerSources.applicationApi.code
sourceSets.main.java.srcDir "${swaggerSources.applicationApi.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.applicationApi.code.outputDir}/src/main/resources"

task deleteSwaggerDefaultApplication(type: Delete) {
    delete "${swaggerSources.applicationApi.code.outputDir}/src/main/java/org/testdashboard/Swagger2SpringBoot.java"
    delete "${swaggerSources.applicationApi.code.outputDir}/src/main/resources/application.properties"
}

generateSwaggerCodeApplicationApi.finalizedBy(deleteSwaggerDefaultApplication)